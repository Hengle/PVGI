// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

// Readable/Writable texture for storing the direct lighting and the occupied flag
RWTexture3D<float4>			voxelGrid;

// Texture for storing the color with direct lighting
Texture2D<float4>			lightingTexture;

// Texture for storing the world space position
Texture2D<float4>			positionTexture;

// Variable representing the dimension of the voxel grid
uint						voxelResolution;

// World-space boundary of the voxel grid
float 						worldVolumeBoundary;

// Function to get position of voxel in the grid
inline uint3 GetVoxelPosition (float3 worldPosition)
{
	float3 encodedPosition = worldPosition / worldVolumeBoundary;
	encodedPosition += float3(1.0f, 1.0f, 1.0f);
	encodedPosition /= 2.0f;
	uint3 voxelPosition = (uint3)(encodedPosition * voxelResolution);
	return voxelPosition;
}

[numthreads(1, 1, 1)]
// Entry point of the voxelization compute shader
void CSMain (uint3 id : SV_DispatchThreadID)
{
	// Color of the current voxel with lighting
	float3 lightingColor = lightingTexture[id.xy].rgb;

	// World space position
	float3 worldPosition = positionTexture[id.xy].rgb;

	// Extract the pixel's depth
	float lindepth = positionTexture[id.xy].a;

	uint3 voxelPosition = GetVoxelPosition(worldPosition);

	// Inject the current voxel's information into the grid
	if (((lindepth < voxelGrid[voxelPosition].a) && (lindepth > 0.01f)) || (voxelGrid[voxelPosition].a == 0.0f))
	{
		voxelGrid[voxelPosition] = float4(lightingColor, lindepth);
	}
}